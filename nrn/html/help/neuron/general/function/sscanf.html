<h1><a name="sscanf">sscanf</a></h1>
<pre>
<a href="../../0nrn.html#functions">functions</a>
</pre>
<h2>SYNTAX</h2>
<code>num = sscanf("string", "format", args)</code><br>
<h2>DESCRIPTION</h2>
A subset of the c stdio sscanf function.
The formats understood are %s, %[...], %c, %lf, %f, %d, %i, %o, %x, along with
numbers and the * character. Explicit strings in the format are also understood.
A great deal of error checking is done to make sure the args match the
format string. The function returns the actual number of args that are
assigned values.
<p>
Note that %lf is always preferred to %f since the latter truncates to floating
point accuracy. Hence
<blockquote><pre>
x=0
sscanf("0.3","%f",&amp;x)
print x
prints the value
0.30000001
</pre></blockquote>
<h2>EXAMPLES</h2>
<a href="sscanf.hoc">execute following example</a>
<blockquote><pre>
strdef s
double x[20]
y=0

sscanf("this is a test\n", "%s", s)
print s

sscanf("this is a test\n", "%[^\n]", s)
print s

sscanf("this is a test\n", "%*s%s", s)
print s

sscanf("this is a test\n", "%2s", s)
print s

sscanf("this is a test\n", "%10s", s)
print s

sscanf("this is a test\n", "%*3s%s", s)
print s

sscanf("this is a test\n", "%*s%s", s)
sscanf("0xff 010 25", "%i%i%i", &amp;x[0], &amp;x[1], &amp;x[2])
print x[0], x[1], x[2]

double x[20]
sscanf("ff 10 25", "%x%o%d", &amp;x[0], &amp;x[1], &amp;x[2])
print x[0], x[1], x[2]

sscanf("50.75", "%d.%d", &amp;x[0], &amp;x[1])
print x[0], x[1]

sscanf("30.987654321", "%f", &amp;y)
printf("%20.15g\n", y)

sscanf("30.987654321", "%lf", &amp;y)
printf("%20.15g\n", y)

sscanf("A", "%c", &amp;y)
printf("%c\n", y)

sscanf("1 2 3 4 5 6 7 8 9 10", "%f%f%f%f%f%f%f%f%f%f%f", &amp;x[0], &amp;x[1], &amp;x[2],\
	&amp;x[3], &amp;x[4], &amp;x[5], &amp;x[6], &amp;x[7], &amp;x[8], &amp;x[9], &amp;x[10], &amp;x[11], &amp;x[12])
print "should only read ten of the following"
for i=0,12 print i, x[i]

print "error tests"

execute1("sscanf(\"string\", \"%\", s)")
execute1("sscanf(\"string\", \"%*\", s)")
execute1("sscanf(\"string\", \"%c%c%c%c%c%c%c%c%c%c%c%c%c%c%c%c%c%c%c%c%c\", &amp;x,&amp;x,&amp;x,&amp;x,&amp;x,&amp;x,&amp;x,&amp;x,&amp;x,&amp;x,&amp;x,&amp;x,&amp;x,&amp;x,&amp;x,&amp;x,&amp;x,&amp;x,&amp;x,&amp;x,&amp;x)")
execute1("sscanf(\"string\", \"%*5\", s)")

execute1("sscanf(\"string\", \"%l[st]\", s)")
execute1("sscanf(\"string\", \"%[\", s)")
execute1("sscanf(\"string\", \"%[]\", s)")
execute1("sscanf(\"string\", \"%[^]\", s)")
execute1("sscanf(\"string\", \"%ls\", s)")
execute1("sscanf(\"string\", \"%lc\", &amp;y)")
execute1("sscanf(\"string\", \"%5c\", &amp;y)")
execute1("sscanf(\"string\", \"%q\", &amp;y)")

execute1("sscanf(\"string\", \"%s\")")

execute1("sscanf(\"string\", \"%s\", &amp;y)")
execute1("sscanf(\"25\", \"%d\", s)")


execute1("sscanf(\"string\", \"%c%c%c%c%c%c%c%c%c%c%c%c%c\", &amp;x,&amp;x,&amp;x,&amp;x,&amp;x,&amp;x,&amp;x,&amp;x,&amp;x,&amp;x,&amp;x,&amp;x,&amp;x)")


</pre></blockquote>
<p><hr><p>
<date>
neuron/general/function/sscanf.hel : Sep 21 09:30
